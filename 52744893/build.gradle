import static org.apache.tools.ant.taskdefs.condition.Os.*

plugins {
  id 'application'
}

repositories {
  mavenLocal()
  mavenCentral()
}

configurations {
  macos
  linux
  windows
}

ext {
  swtVersion = '4.3'
  def swtDependency = 'org.eclipse.swt:org.eclipse.swt'
  swtDependencyMacos = "${swtDependency}.cocoa.macosx.x86_64:${swtVersion}"
  swtDependencyLinux = "${swtDependency}.gtk.linux.x86_64:${swtVersion}"
  swtDependencyWin32 = "${swtDependency}.win32.win32.x86_64:${swtVersion}"
}

dependencies {
  implementation 'com.google.guava:guava:26.0-jre'
  compileOnly determineSWTDependency()
  macos "org.eclipse.swt:org.eclipse.swt.cocoa.macosx.x86_64:${swtVersion}"
  linux "org.eclipse.swt:org.eclipse.swt.gtk.linux.x86_64:${swtVersion}"
  windows "org.eclipse.swt:org.eclipse.swt.win32.win32.x86_64:${swtVersion}"
}

def determineSWTDependency() {
  if (isFamily(FAMILY_WINDOWS)) {
    return swtDependencyWin32
  } else if (isFamily(FAMILY_MAC)) {
    return swtDependencyMacos
  } else if (isFamily(FAMILY_UNIX)) {
    return swtDependencyLinux
  }
}

distributions {
  macos {
    contents content('macos')
  }
  linux {
    contents content('linux')
  }
  windows {
    contents content('windows')
  }
}

def content(conf) {
  def c = {
    with project.distributions.main.contents
    into('lib') {
      from configurations."$conf"
    }
  }
  c
}

mainClassName = 'lol.Lol'
